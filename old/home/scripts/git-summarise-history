#!/usr/bin/env fish

argparse h/help 'a/author=' 'm/model=' -- $argv

if set -ql _flag_h
    echo "usage: aisummarize.fish [-h|--help][-a|--author AUTHOR]"
    return 1
end

set -l model "deepseek-v2:16b"
if set -ql _flag_model
    set model $_flag_model
end
echo "using ollama model $model " >/dev/stderr

set author $_flag_author
echo "fetching commits for author $author" >/dev/stderr

set dates (git log --date=short --pretty=format:'%ad' --author=$author | sort | uniq)

for day in $dates
    set messages (git log --since="$day 00:00" --until="$day 23:59" --pretty=format:'%s' --author=$author)

    set summary (echo "Summarize the following git commit messages in a single sentence of maximum 8 words:\n $messages" |\
        ollama run $model)
    echo "$day: $summary"
end
